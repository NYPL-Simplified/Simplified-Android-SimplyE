def versionValue() {
  def propsFile = file("version.properties")
  def Properties props = new Properties()
  def code
  if (propsFile.canRead()) {
    props.load(new FileInputStream(propsFile))
    code = props["versionCode"].toInteger()
  } else {
    throw new FileNotFoundException("Could not read version.properties!")
  }

  props["versionCode"] = (code + 1).toString()
  props.save(new FileOutputStream(propsFile), "")

  logger.info("incrementing build version ${code} -> ${code + 1}")
  return code
}

android {
  defaultConfig {
    versionCode = versionValue()
    versionName = version
    setProperty("archivesBaseName", "simplye-${version}-${versionCode}")
  }

  packagingOptions {
    doNotStrip 'lib/**/*.so'

    // Readium and the PDF reader both provide this shared library. This causes
    // the build to fail with an error because Gradle doesn't know which to pick.
    pickFirst 'lib/x86/libc++_shared.so'
    pickFirst 'lib/arm64-v8a/libc++_shared.so'
    pickFirst 'lib/armeabi-v7a/libc++_shared.so'
  }

  signingConfigs {
    release {
      keyAlias findProperty("org.librarysimplified.simplye.keyAlias")
      keyPassword findProperty("org.librarysimplified.simplye.keyPassword")
      storeFile file("keystore.jks")
      storePassword findProperty("org.librarysimplified.simplye.storePassword")
    }
  }

  buildTypes {
    debug {
      ndk {
        abiFilters 'x86', 'arm64-v8a', 'armeabi-v7a'
      }
    }
    release {
      ndk {
        abiFilters 'arm64-v8a', 'armeabi-v7a'
      }
      signingConfig signingConfigs.release
    }
  }

  lintOptions {
    checkReleaseBuilds false
  }
}

dependencies {
  implementation libraries.firebaseAnalytics
  implementation libraries.firebaseCrashlytics
  implementation libraries.nyplDRMAdobeProvider
  implementation libraries.nyplDRMCore
  implementation libraries.nyplFindaway
  implementation libraries.simplifiedAccountSourceNYPLRegistry
  implementation libraries.simplifiedAnalyticsCirculation
  implementation libraries.simplifiedAudioBooksOverdrive
  implementation libraries.simplifiedCrashlytics
  implementation libraries.simplifiedMain
  implementation libraries.simplifiedMigrationFrom3Master

  annotationProcessor libraries.googleAutoValue
}
